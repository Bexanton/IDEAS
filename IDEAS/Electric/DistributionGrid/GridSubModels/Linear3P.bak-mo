within IDEAS.Electric.DistributionGrid.GridSubModels;
model Linear3P "Linear single-line grid for 3-phase situation"

// Input parameters //////////////////////////////////////////////////////////////////////////////////////////////
  parameter Integer nLoads = 33 "Number of buildings attached to the feeder";
  parameter Modelica.SIunits.Length[nLines] length = ones(nLoads)*24
    "Length of all (nLoads+1) cables";
  replaceable parameter IDEAS.Electric.Data.Interfaces.Cable cable;

  final parameter Integer nLines = nLoads;

  final parameter Modelica.SIunits.Resistance[3,nLines] R3 = {cable.RCha.*length for i in 1:3};
  final parameter Modelica.SIunits.Reactance[3,nLines] X3 = {cable.XCha.*length for i in 1:3};

// Output variables //////////////////////////////////////////////////////////////////////////////////////////////
  Modelica.SIunits.ReactivePower QGriTot = Modelica.ComplexMath.imag(TraPin.v*Modelica.ComplexMath.conj(TraPin.i))
    "Reactive transformer load";
  Modelica.SIunits.ActivePower PGriTot = Modelica.ComplexMath.real(TraPin.v*Modelica.ComplexMath.conj(TraPin.i))
    "Active transformer load";
  Modelica.SIunits.ActivePower PLosBra[3,nLines] = line.Plos
    "Resistive losses in each line";
  Modelica.SIunits.ActivePower PGriLosTot = sum(PLosBra)
    "Total resistive loss in the feeder";
  Modelica.SIunits.Voltage VMax = max(Modelica.ComplexMath.'abs'(node.v))
    "Maximum momentaneous nodal voltage";
  Modelica.SIunits.Voltage VMin = min(Modelica.ComplexMath.'abs'(node.v))
    "Minimum momentaneous nodal voltage";

// Connection variables //////////////////////////////////////////////////////////////////////////////////////////
  Modelica.Electrical.QuasiStationary.SinglePhase.Interfaces.PositivePin TraPin[3]
    annotation (Placement(transformation(extent={{-110,-10},{-90,10}})));
  Modelica.Electrical.QuasiStationary.SinglePhase.Interfaces.PositivePin TraGnd
    annotation (Placement(transformation(extent={{-110,-30},{-90,-10}})));
  Modelica.Electrical.QuasiStationary.SinglePhase.Interfaces.PositivePin[4, nLoads] node
    annotation (Placement(transformation(extent={{90,-10},{110,10}})));

// Protected variables ///////////////////////////////////////////////////////////////////////////////////////////
protected
  Components.Branch[3,nLines] line(R=R3, X=X3)
    annotation (Placement(transformation(extent={{-10,-10},{10,10}})));
  Components.Branch[nLines] neutral(R=cable.RCha.*length, X=cable.XCha.*length)
    annotation (Placement(transformation(extent={{-10,-30},{10,-10}})));

equation
  connect(line[:,1].pin_p,TraPin) annotation (Line(
      points={{-10,0},{-100,0}},
      color={85,170,255},
      smooth=Smooth.None));
  connect(neutral[1].pin_p,TraGnd) annotation (Line(
      points={{-10,-20},{-100,-20}},
      color={85,170,255},
      smooth=Smooth.None));

  for i in 1:nLines loop
    connect(line[:,i].pin_n,node[1:3,i]) annotation (Line(
      points={{10,0},{100,0},{100,0},{100,-8},{100,-8},{100,-7.5}},
      color={85,170,255},
      smooth=Smooth.None));
    connect(neutral[i].pin_n,node[4,i]) annotation (Line(
      points={{10,-20},{56,-20},{56,7.5},{100,7.5}},
      color={85,170,255},
      smooth=Smooth.None));
  end for;

  for j in 1:nLines-1 loop
    connect(line[:,j].pin_n,line[1:3,j+1].pin_p) annotation (Line(
      points={{10,0},{30,0},{30,10},{-30,10},{-30,0},{-10,0}},
      color={85,170,255},
      smooth=Smooth.None));
    connect(neutral[j].pin_n,neutral[j+1].pin_p) annotation (Line(
      points={{10,-20},{32,-20},{32,-28},{-30,-28},{-30,-20},{-10,-20}},
      color={85,170,255},
      smooth=Smooth.None));
  end for;

  annotation (Diagram(coordinateSystem(preserveAspectRatio=false, extent={{-100,
            -100},{100,100}}),
                      graphics), Icon(graphics={
        Line(
          points={{-22,36},{30,2},{100,0}},
          color={85,170,255},
          smooth=Smooth.Bezier),
        Line(
          points={{30,36},{54,18},{100,4}},
          color={85,170,255},
          smooth=Smooth.Bezier),
        Polygon(
          points={{-32,40},{-32,34},{-4,34},{-4,-80},{4,-80},{4,34},{34,34},{34,
              40},{4,40},{4,46},{-4,46},{-4,40},{-32,40}},
          lineColor={95,95,95},
          smooth=Smooth.None,
          fillPattern=FillPattern.Solid,
          fillColor={95,95,95}),
        Line(
          points={{-102,4},{-46,12},{-28,36}},
          color={85,170,255},
          smooth=Smooth.Bezier),
        Line(
          points={{-100,0},{-12,12},{30,36}},
          color={85,170,255},
          smooth=Smooth.Bezier)}));
end Linear3P;
